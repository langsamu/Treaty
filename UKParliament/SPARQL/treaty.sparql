PREFIX : <https://id.parliament.uk/schema/>
PREFIX id: <https://id.parliament.uk/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?treaty
        a :Treaty, :LaidThing, :WorkPackagedThing ;
        :laidThingName ?laidThingName ;
        :laidThingHasLaying ?laying ;
        :workPackagedThingHasWorkPackage ?workPackage ;
        :treatyHasLeadGovernmentOrganisation ?leadGovernmentOrganisation .

    ?leadGovernmentOrganisation :groupName ?leadGovernmentOrganisationGroupName .

    ?laying
        a :Laying, :BusinessItem ;
        :layingDate ?layingDate ;
        :layingHasLayingBody ?layingBody ;
        :businessItemHasProcedureStep ?procedureStep .

    ?layingBody
        a :Group ;
        :groupName ?layingBodyGroupName .

    ?workPackage
        a :WorkPackage ;
        :workPackageHasBusinessItem ?businessItem ;
        :workPackageHasProcedure ?procedure .

    ?businessItem
        :businessItemDate ?businessItemDate ;
        :businessItemHasBusinessItemWebLink ?businessItemWebLink ;
        :businessItemHasProcedureStep ?BusinessItemProcedureStep .

    ?BusinessItemProcedureStep :procedureStepName ?procedureStepName .

    ?procedure
        a :Procedure ;
        :procedureName ?procedureName .

    ?procedureStep :procedureStepHasHouse ?house .

    ?house :houseName ?houseName .
}
WHERE {
    ?treaty
        a :Treaty ;
        :laidThingName ?laidThingName ;
        :treatyHasLeadGovernmentOrganisation ?leadGovernmentOrganisation .

    ?leadGovernmentOrganisation :groupName ?leadGovernmentOrganisationGroupName .

    OPTIONAL {
        ?treaty
            a :LaidThing ;
            :laidThingHasLaying ?laying .

        ?laying
            :businessItemHasProcedureStep ?procedureStep ;
            :layingDate ?layingDate ;
            :layingHasLayingBody ?layingBody .

        ?layingBody :groupName ?layingBodyGroupName .

        ?procedureStep :procedureStepHasHouse ?house .

        ?house :houseName ?houseName .
    }

    OPTIONAL {
        ?treaty
            a :WorkPackagedThing ;
            :workPackagedThingHasWorkPackage ?workPackage .

        ?workPackage :workPackageHasProcedure ?procedure .

        ?procedure :procedureName ?procedureName .

        OPTIONAL {
            ?workPackage :workPackageHasBusinessItem ?businessItem .

            ?businessItem :businessItemHasProcedureStep ?BusinessItemProcedureStep .

            OPTIONAL { ?businessItem :businessItemDate ?businessItemDate . }

            OPTIONAL { ?businessItem :businessItemHasBusinessItemWebLink ?businessItemWebLink . }

            ?BusinessItemProcedureStep :procedureStepName ?procedureStepName .
        }
    }
}
